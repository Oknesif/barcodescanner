ext {
    versions = [
            compile_sdk    : 31,
            min_sdk        : 14,
            min_compile_sdk: 28,
            target_sdk     : 31,
            zxing          : "3.3.3",
            barcodescanner : "1.9.14",
            annotaions     : "1.3.0",
            app_compat     : "1.4.1",
            material       : "1.5.0"
    ]

    libraries = [
            app_compat          : "androidx.appcompat:appcompat:$versions.app_compat",
            android_annotations : "androidx.annotation:annotation:$versions.annotaions",
            material            : "com.google.android.material:material:$versions.material",
            zxing_core          : "com.google.zxing:core:$versions.zxing",
            barcodescanner_core : "com.github.Oknesif:core:$versions.barcodescanner",
            barcodescanner_zbar : "me.dm7.barcodescanner:zbar:$versions.barcodescanner",
            barcodescanner_zxing: "me.dm7.barcodescanner:zxing:$versions.barcodescanner"
    ]
}

buildscript {
    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

allprojects {
    group = 'me.dm7.barcodescanner'
    version = '1.9.14'

    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        def isAndroidLibrary = "com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)
        def isAndroidApp = "com.android.build.gradle.AppPlugin".equals(plugin.class.name)

        if (isAndroidLibrary || isAndroidApp) {
            project.android {
                compileSdkVersion versions.compile_sdk

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                android {
                    lintOptions {
                        abortOnError false
                    }
                }

                defaultConfig {
                    aarMetadata {
                        minCompileSdk = versions.min_compile_sdk
                    }
                    minSdkVersion versions.min_sdk
                    targetSdkVersion versions.target_sdk
                    versionCode 1914
                    versionName "1.9.14"
                }
            }
        }

        if (isAndroidLibrary) {
            afterEvaluate {
                publishing {
                    publications {
                        release(MavenPublication) {
                            from components.release

                            groupId = 'com.github.Oknesif'
                            artifactId = pomArtifactId
                            version project.version

                            pom {
                                name = pomName
                                description = pomDescription
                            }
                        }
                    }
                }
            }
        }
    }
}
